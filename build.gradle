buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'java-library'

group 'com.github.priytam'
version "${CLOCK_VESRION}"
description = """\
Alarmclock with capabilities of scheduling tasks and execution of recurring and normal task
------------------------------------------
Project version: ${version}
Gradle version: ${gradle.gradleVersion}
Documentation: https://priytam.github.io/clock/
------------------------------------------
"""

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.github.priytam:stopwatch:1.0.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    compile group: 'concurrent', name: 'concurrent', version: '1.3.4'
    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    compile group: 'log4j', name: 'log4j', version: '1.2.16'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

if (project.hasProperty('release')) {

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/Priytam/alarm'

                        scm {
                            connection 'scm:git:https://github.com/Priytam/alarm.git'
                            developerConnection 'scm:git:git@github.com:Priytam/alarm.git'
                            url 'https://github.com/Priytam/alarm.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'Priytam'
                                name = 'Priytam Jee Pandey'
                                email = 'mrpjpandey@gmail.com'
                            }
                        }
                    }
                }
            }
        }

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}

/* use this command to publish library ./gradlew clean build -Prelease uploadArchives */